# Advent of Code (AoC) - day 8, task 2

import "prelude.eg"

using System, OS, List, String (from_chars, to_chars), R = Regex, D = Dict

def run =
    [D VV PC ACC ->
        if not (D::has D PC) then ACC else
        if D::has VV PC then none else
        let VV = D::set VV PC true in
        [("nop",_) -> run D VV (PC+1) ACC
        |("acc",N) -> run D VV (PC+1) (ACC+N)
        |("jmp",N) -> run D VV (PC+N) ACC] (D::get D PC)]

def go =
    [D {X|XX} ->
        let D0 = D::set (D::copy D) X ([("nop",N) -> ("jmp",N)|(_,N) -> ("nop",N)] (D::get D X)) in
        [none -> go D XX|ACC -> ACC] (run D0 D::dict 0 0)]

def main =
    read_lines stdin |> map (R::matches (R::compile "nop|acc|jmp|-?[0-9]+"))
    |> [XX -> zip (from_to 0 (length XX - 1)) (map [{X,Y} -> (X, to_int Y)] XX)]
    |> [XX -> 
        let YY = filter [(_,("nop",_)) -> true|(_,("jmp",_)) -> true|_ -> false] XX in
        go (D::from_list XX) (map fst YY)]
