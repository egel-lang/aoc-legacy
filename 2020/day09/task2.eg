# Advent of Code (AoC) - day 9, task 2

import "prelude.eg"

using System, OS, List, String (from_chars, to_chars), R = Regex, D = Dict

def pairs = [{} -> {} |{X|XX} -> map (tuple X) XX ++ pairs XX]

def l = 25

def go0 = 
    do tails |> filter [XX -> length XX > l] |> map (take (l+1)) |> map [XX -> (last XX, init XX)]
    |> filter [(X,XX) -> not_elem X (map (uncurry (+)) (pairs XX))] |> head |> fst

def go1 =
    [N A {} -> none
    |N A {X|XX} -> 
        let A = A + X in
        if A < N then [none -> none|XX -> {X|XX}] (go1 N A XX) else
        if A == N then {X} else none]

def main =
    read_lines stdin |> map to_int |> [XX -> go0 XX |> [N -> map (go1 N 0) (tails XX)]]
    |> filter ((/=) none) |> head |> sort |> [XX -> head XX + last XX]
